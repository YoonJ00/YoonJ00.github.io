준비물 및 회로 구성<br>
: 준비물은 <b>이더넷 쉴드, LED 1개, 220Ω 저항 1개, 몇개의 점퍼선</b>이 필요하다.<br>
제일 중요한 준비물은 IP주소를 할당받아 인터넷과 연결하기 위해서 필요한 <b>인터넷 공유기</b>이다.<br> 
DHCP서버가 별도로 있다면, 이를 이용하면 된다. 그리고 사용할 수 있는 공인 IP주소 대역을 알아야 한다. PC의 <b>cmd창에서 ipconfig</b> 명령을 내리면 지금 사용하고 있는 IP정보들을 얻을 수 있다.<br><br>
![cmd](https://user-images.githubusercontent.com/59801728/75208543-60a4bb00-57bf-11ea-8815-7d75bd591c25.PNG)<br><br>
참고) 공인 IP주소 대역은 다음과 같이 <b>A, B, C 이렇게 3개의 클래스</b>가 있다.<br>
<b>A 클래스 : 10.0.0.0 ~ 10.255.255.255</b><br>
<b>B 클래스 : 172.16.0.0 ~ 172.31.255.255</b><br>
<b>C 클래스 : 192.168.0.0 ~ 192.168.255.255</b><br><br><br>
준비물<br>
======<br>
이더넷 쉴드 1개<br>
LED 1개<br>
220Ω 저항 1개<br>
여러 점퍼선들<br>
인터넷 공유기<br><br>
회로 구성<br>
========<br>
아두이노 DB <---> 220Ω​ 저항 <---> + LED | - LED <---> GND<br>
*이더넷 쉴드가 점퍼선을 쉽게 연결할 수 없는 구조로 되어 있음<br><br><br>


스케치 작성<br>
: 스케치는 이더넷 라이브러리에서 제공되는 WebServer 스케치를 수정하여 웹 브라우저로 아두이노 디지털 8번 핀에 연결된 LED를 
ON/OFF하게 작성하였다.<br>
 웹 브라우저에서 LED ON/OFF버튼을 눌렀을 떄, LED를 ON/OFF하기 위한 요청을 POST로 보내게 만들어 보았다.<br>
 FORM에서 method를 GET으로 사용하면, URL에 정보가 노출되며, POST로 사용하면 HTTP 헤더에 포함되어 요청된다.


LED ON
 ```
 POST / HTTP/1.1

Accept: text/html, application/xhtml+xml, */*

Referer: http://192.168.0.100/

Accept-Language: ko-KR

User-Agent: Mozilla/5.0 (Windows NT 6.1; WOW64; Trident/7.0; rv:11.0) like Gecko

Content-Type: application/x-www-form-urlencoded

Accept-Encoding: gzip, deflate

Host: 192.168.0.100

Content-Length: 5
 
DNT: 1

Connection: Keep-Alive 

Cache-Control: no-cache

 

do=ON
```
<br>
LED OFF<br>
-------------------------------------------------------------------------------------------------------
```
POST / HTTP/1.1

Accept: text/html, application/xhtml+xml, */*

Referer: http://192.168.0.100/

Accept-Language: ko-KR

User-Agent: Mozilla/5.0 (Windows NT 6.1; WOW64; Trident/7.0; rv:11.0) like Gecko

Content-Type: application/x-www-form-urlencoded

Accept-Encoding: gzip, deflate

Host: 192.168.0.100

Content-Length: 6

DNT: 1

Connection: Keep-Alive

Cache-Control: no-cache



do=OFF
```

1) bind : 웹 서버를 실행하는 것은 서버 소켓을 열고<br>
2) listen : 인터넷에 연결되어 있는 클라이언트들의 접속 요구를 기다리고 <br>
3) accept : 이를 받아들이고<br>
4) read : 받아 들인 소켓으로부터 HTTP요청(request)을 받고<br>
5) write : HTTP요청에 대한 HTTP응답(response)을 소켓으로 전송한다.<br>
6) close : 모든 통신이 끝났을 때, 사용한 소켓을 닫는다.<br>




이 스케치에서도 Ethernet.begin을 이용하여 사용할 IP주소와 MAC주소를 설정하고, 웹 서버 객체인 EthernetServer.begin을 사용하여 내부적으로 listen을 시작하고, EthernetServer.available을 이용하여 클라이언트의 연결 요구를 accept해주고, 클라이언트 소켓에 HTTP요청이 있는지 확인하여 HTTP요청을 읽어들이고 read, 이에 대한 HTTP응답을 만들어 보내는 write로 서버 소켓 서비스와 비슷하게 이루어져 있다.

```
/* 
  Web Server 
  
 A simple web server that turns LED ON/OFF 
  
 Circuit: 
 
 LED is connected to D8 
  
 created 18 Dec 2009 by David A. Mellis 
 modified 9 Apr 2012 by Tom Igoe 
 modified 26 July 2014 by Wayfarer 
  
 */ 
 
#include <SPI.h> 
#include <Ethernet.h> 
 
#define SSD  4 
#define LED  8 
#define ETH 10 
 
// Enter a MAC address and IP address for your controller below. 
// The IP address will be dependent on your local network: 
byte mac[] = {  
  0xDE, 0xAD, 0xBE, 0xEF, 0xFE, 0xED }; 
IPAddress ip(192,168,0,100); 
 
// Initialize the Ethernet server library 
// with the IP address and port you want to use  
// (port 80 is default for HTTP): 
EthernetServer server(80); 
String line; 
 
boolean bLED = false; 
boolean bCNT = false; 
int length = 0; 
 
void setup() { 
 // Open serial communications and wait for port to open: 
  Serial.begin(9600); 
   while (!Serial) { 
    ; // wait for serial port to connect. Needed for Leonardo only 
  } 
 
  // start the Ethernet connection and the server: 
  Ethernet.begin(mac, ip); 
  server.begin(); 
  Serial.print("server is at "); 
  Serial.println(Ethernet.localIP()); 
  line.reserve(200); 
  pinMode(LED, OUTPUT); 
  pinMode(SSD, OUTPUT); 
  digitalWrite(LED, LOW); 
  digitalWrite(SSD, HIGH);   
} 
 
void loop() { 
   
  // listen for incoming clients 
  EthernetClient client = server.available(); 
  if (client) { 
    Serial.println(F("new client")); 
    // an http request ends with a blank line 
    boolean currentLineIsBlank = true; 
    line = ""; 
    while (client.connected()) { 
      if (client.available()) { 
        char c = client.read(); 
         
        line += c; 
         
        // if you've gotten to the end of the line (received a newline 
        // character) and the line is blank, the http request has ended, 
        // so you can send a reply 
        if (c == '\n' && currentLineIsBlank ) { 
          Serial.println(); 
          if(bCNT) { 
            line = ""; 
            for(int i=0; i < length; i++) { 
              c = client.read(); 
              line += c; 
            } 
             
            Serial.println(line); 
             
            if(line.indexOf("do=ON") >= 0) { 
              bLED = true; 
              digitalWrite(LED, HIGH); 
              Serial.println(F("LED is ON")); 
            } 
             
            else if(line.indexOf("do=OFF") >= 0) { 
              bLED = false; 
              digitalWrite(LED, LOW); 
              Serial.println(F("LED is OFF")); 
            }            
          } 
           
          line = ""; 
          bCNT = false; 
           
          // send a standard http response header 
          client.println(F("HTTP/1.1 200 OK")); 
          client.println(F("Content-Type: text/html")); 
          client.println(F("Connection: close"));  // the connection will be closed after completion of the response 
          client.println(F("Refresh: 10"));  // refresh the page automatically every 10 sec 
          client.println(); 
          client.println(F("<!DOCTYPE HTML>")); 
          client.println(F("<html>")); 
          client.println(F("  <head>")); 
          client.println(F("    <meta http-equiv=\"Content-Type\" content=\"text/html;charset=UTF-8\">")); 
          client.println(F("  </head>")); 
          client.println(F("  <body>")); 
          client.println(F("    <p>아두이노 웹 서버<br>")); 
          client.println(F("       ================</p>")); 
          client.println(F("    <form method=\"post\">")); 
          if(!bLED) { 
            client.println(F("      <input type=\"hidden\" name=\"do\" value=\"ON\">")); 
            client.println(F(" LED     <input type=\"button\" name=\"button1\" value=\"ON\" onclick=\"submit();\" >")); 
          } 
          else { 
            client.println(F("      <input type=\"hidden\" name=\"do\" value=\"OFF\">")); 
            client.println(F(" LED     <input type=\"button\" name=\"button1\" value=\"OFF\" onclick=\"submit();\">")); 
          } 
          client.println(F("    </form>")); 
          client.println(F("  </body>")); 
          client.println(F("</html>")); 
          break; 
        } 
         
        if (c == '\n') { 
          Serial.print(line); 
           
          // you're starting a new line 
          currentLineIsBlank = true; 
           
          if(line.indexOf("Content-Length:") >= 0) { 
            String s = line.substring(line.indexOf(":") + 1); 
            s.trim(); 
             
            length = s.toInt();           
            bCNT = true; 
          }           
          line=""; 
        } 
         
        else if (c != '\r') { 
          // you've gotten a character on the current line 
          currentLineIsBlank = false; 
        } 
      } 
    } 
    // give the web browser time to receive the data 
    delay(1); 
//* 
    // close the connection: 
    client.stop(); 
    Serial.println("client disonnected"); 
//*/ 
  } 
} 
```



